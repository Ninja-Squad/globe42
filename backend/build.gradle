buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/libs-milestone' }
    }

    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.0.0.RC2'
        classpath 'org.flywaydb:flyway-gradle-plugin:5.0.3'
        classpath 'org.postgresql:postgresql:9.4.1212'
    }
}

plugins {
    id "org.flywaydb.flyway" version "5.0.3"
    id "com.gorylenko.gradle-git-properties" version "1.4.17"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/libs-milestone" }
}

tasks.withType(JavaCompile) {
    // enable parameter name preservation, used by Jackson
    options.compilerArgs.add('-parameters')

    // disable annotation processing which generates a gradle warning
    options.compilerArgs.add('-proc:none')
}

bootJar {
    archiveName = 'globe42.jar'
    dependsOn ':frontend:npm_run_build'

    into('BOOT-INF/classes/static') {
        from "${project(':frontend').projectDir}/dist"
    }
}

bootRun {
    args '--spring.profiles.active=demo'
    args '--globe42.secretKey=QMwbcwa19VV02Oy5T7LSWyV+/wZrOsRRfhCR6TkapsY='
    args '--globe42.googleCloudStorageCredentialsPath=' + rootProject.file('secrets/google-cloud-storage-dev.json')
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'io.jsonwebtoken:jjwt:0.7.0'
    compile 'com.google.cloud:google-cloud-storage:1.5.1'

    testCompile('org.springframework.boot:spring-boot-starter-test') {
        exclude module: 'junit'
    }
    testCompile 'com.ninja-squad:DbSetup:2.1.0'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.2'
    // To avoid compiler warnings about @API annotations in JUnit code.
    testCompile('org.apiguardian:apiguardian-api:1.0.0')

    runtime 'org.postgresql:postgresql:42.1.4'
    runtime 'org.flywaydb:flyway-core:4.2.0' // old version needed because Clever cloud only supports postgresql 9.2, and Flyway 5 doesn't support 9.2 anymore

    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.0.2'
}

test {
    useJUnitPlatform()
}

// remove default tasks added by flyway plugin
tasks.removeIf { it.name.startsWith('flyway') }

task flywayCleanInteg(type: org.flywaydb.gradle.task.FlywayCleanTask) {
    url = 'jdbc:postgresql://localhost:5432/globe42'
    user = 'globe42'
    password = 'globe42'
}

task flywayCleanTest(type: org.flywaydb.gradle.task.FlywayCleanTask) {
    url = 'jdbc:postgresql://localhost:5432/globe42_test'
    user = 'globe42'
    password = 'globe42'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

springBoot {
    buildInfo()
}
